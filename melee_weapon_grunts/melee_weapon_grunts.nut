// Squirrel
// Melee Weapon Grunts

__tMeleeWeaponGrunts <-
{
	sCoachSound =
	[
		"player/survivor/voice/coach/meleeswing01.wav"
		"player/survivor/voice/coach/meleeswing02.wav"
		"player/survivor/voice/coach/meleeswing03.wav"
		"player/survivor/voice/coach/meleeswing04.wav"
		"player/survivor/voice/coach/meleeswing05.wav"
		"player/survivor/voice/coach/meleeswing06.wav"
		"player/survivor/voice/coach/meleeswing07.wav"
		"player/survivor/voice/coach/meleeswing08.wav"
		"player/survivor/voice/coach/meleeswing09.wav"
		"player/survivor/voice/coach/meleeswing10.wav"
		"player/survivor/voice/coach/meleeswing11.wav"
		"player/survivor/voice/coach/meleeswing12.wav"
		"player/survivor/voice/coach/meleeswing13.wav"
		"player/survivor/voice/coach/meleeswing14.wav"
		"player/survivor/voice/coach/meleeswing15.wav"
		"player/survivor/voice/coach/meleeswing16.wav"
		"player/survivor/voice/coach/meleeswing17.wav"
		"player/survivor/voice/coach/meleeswing18.wav"
		"player/survivor/voice/coach/meleeswing19.wav"
	]

	sEllisSound =
	[
		"player/survivor/voice/mechanic/meleeswing01.wav"
		"player/survivor/voice/mechanic/meleeswing02.wav"
		"player/survivor/voice/mechanic/meleeswing03.wav"
		"player/survivor/voice/mechanic/meleeswing04.wav"
		"player/survivor/voice/mechanic/meleeswing05.wav"
		"player/survivor/voice/mechanic/meleeswing06.wav"
		"player/survivor/voice/mechanic/meleeswing07.wav"
	]

	sNickSound =
	[
		"player/survivor/voice/gambler/meleeswing01.wav"
		"player/survivor/voice/gambler/meleeswing02.wav"
		"player/survivor/voice/gambler/meleeswing03.wav"
		"player/survivor/voice/gambler/meleeswing04.wav"
		"player/survivor/voice/gambler/meleeswing05.wav"
		"player/survivor/voice/gambler/meleeswing06.wav"
		"player/survivor/voice/gambler/meleeswing07.wav"
		"player/survivor/voice/gambler/meleeswing08.wav"
		"player/survivor/voice/gambler/meleeswing09.wav"
		"player/survivor/voice/gambler/meleeswing10.wav"
		"player/survivor/voice/gambler/meleeswing11.wav"
		"player/survivor/voice/gambler/meleeswing12.wav"
		"player/survivor/voice/gambler/meleeswing13.wav"
		"player/survivor/voice/gambler/meleeswing14.wav"
		"player/survivor/voice/gambler/meleeswing15.wav"
	]

	sRochelleSound =
	[
		"player/survivor/voice/producer/meleeswing01.wav"
		"player/survivor/voice/producer/meleeswing02.wav"
		"player/survivor/voice/producer/meleeswing03.wav"
		"player/survivor/voice/producer/meleeswing04.wav"
		"player/survivor/voice/producer/meleeswing05.wav"
		"player/survivor/voice/producer/meleeswing06.wav"
		"player/survivor/voice/producer/meleeswing07.wav"
		"player/survivor/voice/producer/meleeswing08.wav"
		"player/survivor/voice/producer/meleeswing09.wav"
	]

	sLouisSound =
	[
		"player/survivor/voice/manager/hurtminor02.wav"
		"player/survivor/voice/manager/hurtminor05.wav"
		"player/survivor/voice/manager/hurtminor06.wav"
		"player/survivor/voice/manager/shoved01.wav"
		"player/survivor/voice/manager/shoved02.wav"
		"player/survivor/voice/manager/shoved03.wav"
		"player/survivor/voice/manager/shoved04.wav"
		"player/survivor/voice/manager/shoved05.wav"
	]

	sZoeySound =
	[
		"player/survivor/voice/teengirl/hordeatttack10.wav"
		"player/survivor/voice/teengirl/hordeattack29.wav"
		"player/survivor/voice/teengirl/hurtminor03.wav"
		"player/survivor/voice/teengirl/shoved01.wav"
		"player/survivor/voice/teengirl/shoved02.wav"
		"player/survivor/voice/teengirl/shoved03.wav"
		"player/survivor/voice/teengirl/shoved04.wav"
		"player/survivor/voice/teengirl/shoved05.wav"
		"player/survivor/voice/teengirl/shoved06.wav"
		"player/survivor/voice/teengirl/shoved14.wav"
	]

	sFrancisSound =
	[
		"player/survivor/voice/biker/hurtminor02.wav"
		"player/survivor/voice/biker/hurtminor04.wav"
		"player/survivor/voice/biker/hurtminor07.wav"
		"player/survivor/voice/biker/hurtminor08.wav"
		"player/survivor/voice/biker/positivenoise02.wav"
		"player/survivor/voice/biker/shoved01.wav"
		"player/survivor/voice/biker/shoved02.wav"
		"player/survivor/voice/biker/shoved03.wav"
		"player/survivor/voice/biker/shoved04.wav"
		"player/survivor/voice/biker/shoved05.wav"
		"player/survivor/voice/biker/shoved06.wav"
		"player/survivor/voice/biker/shoved07.wav"
	]

	sBillSound =
	[
		"player/survivor/voice/namvet/hurtminor02.wav"
		"player/survivor/voice/namvet/hurtminor05.wav"
		"player/survivor/voice/namvet/hurtminor07.wav"
		"player/survivor/voice/namvet/hurtminor08.wav"
		"player/survivor/voice/namvet/shoved01.wav"
		"player/survivor/voice/namvet/shoved02.wav"
		"player/survivor/voice/namvet/shoved03.wav"
		"player/survivor/voice/namvet/shoved04.wav"
		"player/survivor/voice/namvet/shoved05.wav"
		"player/survivor/voice/namvet/positivenoise03.wav"
		"player/survivor/voice/namvet/reactionstartled01.wav"
	]

	sEllisResponse = 
	[
		"player/survivor/voice/mechanic/meleeresponse01.wav"
		"player/survivor/voice/mechanic/meleeresponse02.wav"
		"player/survivor/voice/mechanic/meleeresponse03.wav"
		"player/survivor/voice/mechanic/meleeresponse04.wav"
		"player/survivor/voice/mechanic/meleeresponse05.wav"
		"player/survivor/voice/mechanic/meleeresponse06.wav"
		"player/survivor/voice/mechanic/meleeresponse07.wav"
		"player/survivor/voice/mechanic/meleeresponse08.wav"
		"player/survivor/voice/mechanic/meleeresponse09.wav"
		"player/survivor/voice/mechanic/meleeresponse10.wav"
	]

	sEllisScenes = 
	[
		"scenes/mechanic/meleeresponse01.vcd"
		"scenes/mechanic/meleeresponse02.vcd"
		"scenes/mechanic/meleeresponse03.vcd"
		"scenes/mechanic/meleeresponse04.vcd"
		"scenes/mechanic/meleeresponse05.vcd"
		"scenes/mechanic/meleeresponse06.vcd"
		"scenes/mechanic/meleeresponse07.vcd"
		"scenes/mechanic/meleeresponse08.vcd"
		"scenes/mechanic/meleeresponse09.vcd"
		"scenes/mechanic/meleeresponse10.vcd"
	]

	Precache = function()
	{
		foreach (soundFile in sCoachSound)
			PrecacheSound(soundFile);
		
		foreach (soundFile in sEllisSound)
			PrecacheSound(soundFile);
		
		foreach (soundFile in sNickSound)
			PrecacheSound(soundFile);
		
		foreach (soundFile in sRochelleSound)
			PrecacheSound(soundFile);
		
		foreach (soundFile in sLouisSound)
			PrecacheSound(soundFile);
		
		foreach (soundFile in sZoeySound)
			PrecacheSound(soundFile);
		
		foreach (soundFile in sFrancisSound)
			PrecacheSound(soundFile);

		foreach (soundFile in sBillSound)
			PrecacheSound(soundFile);
		
		foreach (soundFile in sEllisResponse)
			PrecacheSound(soundFile);
	}
	
	ParseConfigFile = function()
	{
		local tData;

		local function Clamp(n, min, max)
		{
			return n < min ? min : (n > max ? max : n);
		}

		local function SerializeSettings()
		{
			local sData = "{";
			foreach (key, val in ::__tMeleeWeaponGrunts.Settings)
			{
				switch (typeof val)
				{
				case "string":
					sData = format("%s\n\t%s = \"%s\"", sData, key, val);
					break;
				
				case "float":
					sData = format("%s\n\t%s = %f", sData, key, val);
					break;
				
				case "integer":
				case "bool":
					sData = sData + "\n\t" + key + " = " + val;
					break;
				}
			}
			sData = sData + "\n}";
			StringToFile("melee_weapon_grunts/settings.cfg", sData);
		}

		if (tData = FileToString("melee_weapon_grunts/settings.cfg"))
		{
			try {
				tData = compilestring("return " + tData)();
				foreach (key, val in Settings)
				{
					if (tData.rawin(key))
					{
						Settings[key] = ((key == "CombatGruntChance" || key == "EllisCommentChance") ? Clamp(tData[key], 1, 100) : tData[key]);
					}
				}
			}
			catch (error) {
				SerializeSettings();
			}
		}
		else
		{
			SerializeSettings();
		}
	}

	IsActorBusy = function(hPlayer)
	{
		return hPlayer.GetCurrentScene() != null;
	}

	OnInfectedDecapitated = function(hPlayer)
	{
		local sModel = NetProps.GetPropString(hPlayer, "m_ModelName");

		if (Settings.VocalizeEllisComment && sModel.len() > 29 && sModel[29] == 104 && RandomInt(1, 100) <= Settings.EllisCommentChance && !IsActorBusy(hPlayer))
			hPlayer.PlayScene(sEllisScenes[RandomInt(0, sEllisScenes.len() - 1)], 0.0);
	}

	EmitSoundOnPlayer = function(hPlayer, sSound, iRadius = 1500.0)
	{
		local hEntity = SpawnEntityFromTable("ambient_generic", {
			origin = (hPlayer.GetOrigin() + hPlayer.GetVelocity() * 0.15)
			message = sSound
			radius = iRadius
			spawnflags = 48
			health = 100
		});

		DoEntFire("!self", "PlaySound", "", 0.0, null, hEntity);
		DoEntFire("!self", "Kill", "", 0.0, null, hEntity);
	}

	OnGameEvent_weapon_fire = function(tParams)
	{
		if (tParams["weapon"] == "melee")
		{
			local aSounds;
			local bL4D2Survivor = true;
			local hPlayer = GetPlayerFromUserID(tParams["userid"]);
			local sModel = NetProps.GetPropString(hPlayer, "m_ModelName");

			if (Time() < flCooldown[hPlayer.GetEntityIndex()] || RandomInt(1, 100) > Settings.CombatGruntChance || !hPlayer.IsSurvivor()
				|| IsActorBusy(hPlayer) || hPlayer.IsDead() || hPlayer.IsDying() || hPlayer.IsIncapacitated() || sModel.len() < 30)
				return;

			switch (sModel[29])
			{
			case 99: // coach
				aSounds = sCoachSound;
				break;

			case 104: // ellis
				aSounds = sEllisSound;
				break;

			case 98: // nick
				aSounds = sNickSound;
				break;

			case 100: // rochelle
				aSounds = sRochelleSound;
				break;

			case 97: // louis
				bL4D2Survivor = false;
				aSounds = sLouisSound;
				break;

			case 110: // zoey
				bL4D2Survivor = false;
				aSounds = sZoeySound;
				break;

			case 101: // francis
				bL4D2Survivor = false;
				aSounds = sFrancisSound;
				break;

			case 118: // bill
				bL4D2Survivor = false;
				aSounds = sBillSound;
				break;

			default:
				return;
			}

			if (bL4D2Survivor)
			{
				if (!Settings.VocalizeL4D2Survivors)
					return;
			}
			else if (!Settings.VocalizeL4D1Survivors)
			{
				return;
			}

			EmitSoundOnPlayer(hPlayer, aSounds[RandomInt(0, aSounds.len() - 1)]);
			flCooldown[hPlayer.GetEntityIndex()] = Time() + 0.5;
		}
	}

	OnGameEvent_infected_decapitated = function(tParams)
	{
		local hPlayer = GetPlayerFromUserID(tParams["userid"]);
		if (hPlayer) OnInfectedDecapitated(hPlayer);
	}

	OnGameEvent_bot_player_replace = function(tParams)
	{
		local hPlayer = GetPlayerFromUserID(tParams["player"]);
		flCooldown[hPlayer.GetEntityIndex()] = Time() + 1.0; // bugged game event firing for melee weapons
	}

	Settings =
	{
		CombatGruntChance = 100
		EllisCommentChance = 40
		VocalizeL4D1Survivors = true
		VocalizeL4D2Survivors = true
		VocalizeEllisComment = true
	}

	flCooldown = array(33, 0.0)
};

__tMeleeWeaponGrunts.Precache();
__tMeleeWeaponGrunts.ParseConfigFile();

__CollectEventCallbacks(__tMeleeWeaponGrunts, "OnGameEvent_", "GameEventCallbacks", RegisterScriptGameEventListener);

printl("[Melee Weapon Grunts]\nAuthor: Sw1ft\nVersion: 1.0.1");